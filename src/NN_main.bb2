; =============================================================================
; NN_main
; -----------------------------------------------------------------------------
; Where the magic happens.
; =============================================================================

AMIGA
If NOT NTSC Then Poke $DFF1DC, $000

; PAL has 256 vertical lines, but NTSC only has 200. Center the display
; so that it doesn't look terrible for PAL.
DEFTYPE .w screen_offset
screen_offset = ((DispHeight - 200) LSR 1) + #CP_origin
!DB_log_val{"MAIN", "screen_offset %d", screen_offset}
InitCopList #CP_coplist, 44, #NN_screen_height, #CP_flags, #CP_sprites, #NN_screen_colors, 0


; Set up a buffer for blitting into the main screen
Buffer #BF_main_screen, 64000
Buffer #BF_shapes, 64000
Buffer #BF_faces, 16000

;Load the title song
DEFTYPE .b success
success = LoadTrackerModule(0, "data/theme.mod")
If success = True
  success = StartTracker(0)
  If success = False Then !DB_warn{"MAIN", "Could not play title song."}
Else
  !DB_warn{"MAIN", "Could not load title song."}
End If

; Load the splash screen
BitMap #BMP_main, #NN_screen_width, #NN_screen_height, #NN_screen_bpp
LoadBitMap #BMP_main, "data/mpg.iff", #PAL_main

LoadShapes #NN_menu_shape, #NN_menu_shape, "data/menu.shapes"

; Display the splash screen
BLITZ
CreateDisplay #CP_coplist
Poke $DFF1DC, $000
; Force the machine to enter NTSC mode

DisplayBitMap #CP_coplist, #BMP_main
FadeInScreen{#CP_coplist, #PAL_main, #PAL_fx}

; Load the game
QAMIGA
success = NN_initialize{"gutter.neon", "pointers.shape"}
If success <> #NN_NEON_success
  !DB_error{"MAIN", "Aborting."}
  Goto Finish
End If

TX_initialize{"display.font", "frame.shape"}

VWait 180

; Fade out the splash screen
BLITZ
FadeOutScreen{#CP_coplist, #PAL_fx, #PAL_main}

; Load the title screen
QAMIGA
NN_load_location{0, 0}

; Run the game
DEFTYPE .w overlapped, prev_overlap, menu_x, menu_y
DEFTYPE .NN_region *region
DEFTYPE .b left_mouse, prev_bmp
DEFTYPE .s scene_description

NN_pointer_id = 0
overlapped = -1
prev_overlap = -1
NN_should_quit = False

BLITZ
Mouse On

NS_script_queue = -1
MS_current_track_id = -1

menu_x = 320 - ShapeWidth(#NN_menu_shape) - 4
menu_y = 200 - ShapeHeight(#NN_menu_shape) - 4

NN_fade_in = False
NN_fade_out = False

prev_bmp = NN_current_bmp

Repeat
  VWait

  left_mouse = IN_update_mouse{}

  ;NN_current_bmp = *NN_current_scene\background_id

  ; If we changed locations, let's kill any text that might be hanging around
  If prev_bmp <> NN_current_bmp
    UnBuffer 0
    prev_bmp = NN_current_bmp
  End If

  ; Show the background image
  DisplayPalette #CP_coplist, NN_current_bmp
  DisplayBitMap #CP_coplist, NN_current_bmp

  ; Update the mouse pointer
  DisplaySprite #CP_coplist, NN_pointer_id, MouseX, MouseY, 0

  ; Check if we should display the scene's first time description
  If NOT NS_scene_flags(NN_current_global_scene_id)
    If *NN_current_scene\first_desc_id <> -1
      For d = *NN_current_scene\first_desc_id to *NN_current_scene\last_desc_id
        scene_description = !__get_string{d}
        TX_draw_text_wrapped{scene_description, 32, #NN_half_screen_width , #NN_screen_height, True}
      Next
    End If
    NS_scene_flags(NN_current_global_scene_id) = True
  End If

  ; Check if menu icon is clicked
  If MouseX > menu_x AND MouseY > menu_y
    DisplaySprite #CP_coplist, #NN_pointer_interact - #NN_pointer_start, MouseX, MouseY, 0
    If left_mouse = #IN_mouse_released
      DG_show_dialog{0}
    End If
  Else
    ; Check if any regions that have been hovered over
    overlapped = NN_get_overlapped_region{MouseX, MouseY}

    If NN_fade_in Then FadeInScreen{#CP_coplist, NN_current_bmp, #PAL_fx}
    If NN_fade_out Then FadeOutScreen{#CP_coplist, NN_current_bmp, #PAL_fx}

    NS_process_queue{}

    If overlapped <> -1
      *region = !__get_region{overlapped}
      NN_pointer_id = *region\pointer_id

      If left_mouse = #IN_mouse_released
        If *region\script_offset <> -1
          UnBuffer #BF_main_screen
          NS_execute_script{*region\script_offset}
        End If

        If *region\goto_scene <> -1
          NN_game_set_scene{*region\goto_scene}
        End If
      Else
        If overlapped <> prev_overlap AND *region\description_id <> -1
          UnBuffer #BF_main_screen
          TX_draw_text_wrapped{!__get_string{*region\description_id}, 32, #NN_half_screen_width , #NN_screen_height, False}
          ;TX_draw_textid_wrapped{*region\description_id, 32, #NN_half_screen_width , #NN_screen_height}
        End If
      End If
    Else
      UnBuffer 0
      NN_pointer_id = 0
    End If
  End If

  prev_overlap = overlapped

  ; Debug commands
CNIF #DEBUG = -1
  If RawStatus($45) Then NN_should_quit = True
  If RawStatus($01)
    For f = 0 to 16
      l$ = Str$(f * 4   ) + ": " + Str$(NS_flags(f* 4    )) + "    "
      l$ + Str$(f* 4 +  1) + ": " + Str$(NS_flags(f* 4 +  1)) + "    "
      l$ + Str$(f* 4 +  2) + ": " + Str$(NS_flags(f* 4 +  2)) + "    "
      l$ + Str$(f* 4 + 3) + ": " + Str$(NS_flags(f* 4 + 3)) + "    "
      !DB_log_str{"MAIN", l$}
    Next
  End If
  If RawStatus($02)
    PrintFreeMem{}
  End If
CEND

Until NN_should_quit

Finish:

If success = #NN_NEON_success
  TX_uninitialize{}
  NN_uninitialize{}
End If

StopTracker

; We're done!
End